\label{xmark-queries}
	\begin{enumerate}[label=Q\arabic*.]
		\paragraph{exact Match}
		\item %1
			Return the name of the person with ID "person0". 
		\begin{lstlisting}[style=XQuery]
			let $auction := doc("xmark.xml") return
				for $b in $auction/site/people/person[@id = "person0"]
			return $b/name/text()
		\end{lstlisting}
		\item %2
			Return the initial increases of all open auctions.
		\begin{lstlisting}[style=XQuery]
			let $auction := doc("xmark.xml") return
    			for $b in $auction/site/open_auctions/open_auction
    			return
                <increase>{ $b/bidder[1]/increase/text() }</increase>
		\end{lstlisting}
		\item %3
			Return the IDs of all open auctions whose current increase is at least twice as high as the initial increase. 
			\begin{lstlisting}[style=XQuery]
				let $auction := doc("xmark.xml") return
					for $b in $auction/site/open_auctions/open_auction
					where zero-or-one($b/bidder[1]/increase/text()) * 2 <=
					$b/bidder[last()]/increase/text()
				return <increase first="{$b/bidder[1]/increase/text()}"
				last="{$b/bidder[last()]/increase/text()}"/>
			\end{lstlisting}	
		
		\item %4
			List the reserves of those open auctions where a certain person issued a bid before another person.
			\begin{lstlisting}[style=XQuery]
			let $auction := doc("xmark.xml") return
			for $b in $auction/site/open_auctions/open_auction
			where some $pr1 in $b/bidder/personref[@person = "person20"],
			$pr2 in $b/bidder/personref[@person = "person51"]
			satisfies $pr1 << $pr2
			return <history>{$b/reserve/text()}</history>
			\end{lstlisting}	
		
		\item %5
			How many sold items cost more than 40?		
		\begin{lstlisting}[style=XQuery]
			let $auction := doc("xmark.xml") return
			count(for $i in $auction/site/closed_auctions/closed_auction
			where $i/price/text() >= 40
			return $i/price)
		\end{lstlisting}	
		\item %6
			How many items are listed on all continents?
		\begin{fakeXML}
			let $auction := doc("xmark.xml") return
			for $b in $auction//site/regions
			return count($b//item)
		\end{fakeXML}	
		\item %7
			How many pieces of prose are in our database?
		\begin{fakeXML}
			let $auction := doc("xmark.xml") return
			for $p in $auction/site
			return count($p//description) + count($p//annotation) + count($p//emailaddress)
		\end{fakeXML}	
		\item %8
			List the names of persons and the number of items they bought.
			
		\begin{fakeXML}
			let $auction := doc("auction.xml") return
				for $p in $auction/site/people/person
				let $a :=
				for $t in $auction/site/closed_auctions/closed_auction
				where $t/buyer/@person = $p/@id
				return $t
			return <item person="{$p/name/text()}">{count($a)}</item>
		\end{fakeXML}	
		\item %9
			List the names of persons and the names of the items they bought in Europe.
		\begin{fakeXML}
			let $auction := doc("xmark.xml") return
			let $ca := $auction/site/closed_auctions/closed_auction return
			let $ei := $auction/site/regions/europe/item
			for $p in $auction/site/people/person
			let $a := for $t in $ca
			where $p/@id = $t/buyer/@person
			return let $n := for $t2 in $ei
			where $t/itemref/@item = $t2/@id
			return $t2
			return <item>{ $n/name/text() }</item>
			return <person name="{ $p/name/text() }">{ $a }</person>
		\end{fakeXML}	
		\item %10
			List all persons according to their interest; use French markup in the result.
		\begin{lstlisting}[style=XQuery,float=ht]
			let $auction := doc($doc)
              for $i in distinct-values($auction/site/people/person/profile/interest/@category)
              let $p :=
                for $t in $auction/site/people/person
                where $t/profile/interest/@category = $i
                return <personne>
              <statistiques>
                <sexe> { $t/profile/gender/text() } </sexe>
                <age> { $t/profile/age/text() } </age>
                <education> { $t/profile/education/text() } </education>
                <revenu> { fn:data($t/profile/@income) } </revenu>
              </statistiques>
              <coordonnees>
                <nom> { $t/name/text() } </nom>
                <rue> { $t/address/street/text() } </rue>
                <ville> { $t/address/city/text() } </ville>
                <pays> { $t/address/country/text() } </pays>
                <reseau>
                  <courrier> { $t/emailaddress/text() } </courrier>
                  <pagePerso> { $t/homepage/text() } </pagePerso>
                </reseau>
              </coordonnees>
              <cartePaiement> { $t/creditcard/text() } </cartePaiement>
            </personne>
              return <categorie>
              <id> { $i } </id>
              { $p }
            </categorie>
		\end{lstlisting}	
		\item  %11
			For each person, list the number of items currently on sale whose price does not exceed 0.02\% of the person’s income.
		\begin{fakeXML}
			let $auction := doc("auction.xml") return
				for $p in $auction/site/people/person
				let $l :=
				for $i in $auction/site/open_auctions/open_auction/initial
				where $p/profile/@income > 5000 * exactly-one($i/text())
				return $i
			return <items name="{$p/name/text()}">{count($l)}</items>
		\end{fakeXML}	
		\item %12
		For each richer-than-average person, list the number of items currently on sale whose price does not exceed 0.02\% of the person’s income.
		
		\begin{fakeXML}
			let $auction := doc("auction.xml") return
			for $p in $auction/site/people/person
			let $l :=
			for $i in $auction/site/open_auctions/open_auction/initial
			where $p/profile/@income > 5000 * exactly-one($i/text())
			return $i
			where $p/profile/@income > 50000
			return <items person="{$p/profile/@income}">{count($l)}</items>
		\end{fakeXML}	
		\item %13
			List the names of items registered in Australia along with their descriptions.
		\begin{fakeXML}
			let $auction := doc("xmark.xml") return
			for $i in $auction/site/regions/australia/item
			return <item name="{ $i/name/text() }">{ $i/description }</item>
		\end{fakeXML}	
		\item %14
			Return the names of all items whose description contains the word ‘gold’.
		\begin{fakeXML}
			let $auction := doc("xmark.xml") return
			for $i in $auction/site//item
			where contains(string(exactly-one($i/description)), "gold")
			return $i/name/text()
		\end{fakeXML}	
		\item %15
			Print the keywords in emphasis in annotations of closed auctions.
		\begin{fakeXML}
			let $auction := doc("xmark.xml") return
			for $a in $auction/site/closed_auctions/closed_auction/annotation/description/
			parlist/listitem/parlist/listitem/text/emph/keyword/text()
			return <text>{ $a }</text
		\end{fakeXML}	
		\item %16
			Return the IDs of those auctions that have one or more keywords in emphasis.
		\begin{fakeXML}
			let $auction := doc("xmark.xml") return
			for $a in $auction/site/closed_auctions/closed_auction
			where not(empty($a/annotation/description/parlist/listitem/parlist/listitem/text/
			emph/keyword/text()))
			return <person id="{ $a/seller/@person }"/>
		\end{fakeXML}	
		\item %17	
			Which persons don’t have a homepage?
		\begin{fakeXML}
			let $auction := doc("xmark.xml") return
			for $p in $auction/site/people/person
			where empty($p/homepage/text())
			return <person name="{ $p/name/text() }"/>
		\end{fakeXML}	
		\item %18
			Convert the currency of the reserve of all open auctions to another currency.
		\begin{fakeXML}
			declare namespace local = "http://www.foobar.org";
			declare function local:convert($v as xs:decimal?) as xs:decimal? {
				2.20371 * $v (: convert Dfl to Euro :)
			};
			let $auction := doc("xmark.xml") return
			for $i in $auction/site/open_auctions/open_auction
			return local:convert(zero-or-one($i/reserve))
		\end{fakeXML}	
		\item %19
			Give an alphabetically ordered list of all items along with their location.
		\begin{fakeXML}
			let $auction := doc("xmark.xml") return
			for $b in $auction/site/regions//item
			let $k := $b/name/text()
			order by zero-or-one($b/location) ascending empty greatest
			return <item name="{ $k }">{ $b/location/text() }</item>
		\end{fakeXML}	
		\item %20
			Group customers by their income and output the cardinality of each group.
		\begin{fakeXML}
			let $auction := doc("xmark.xml") return
			<result>
			<preferred>{
				count($auction/site/people/person/profile[@income >= 100000])
			}</preferred>
			<standard>{
				count($auction/site/people/person/
				profile[@income < 100000 and @income >= 30000])
			}</standard>
			<challenge>{
				count($auction/site/people/person/profile[@income < 30000])
			}</challenge>
			<na>{
				count(for $p in $auction/site/people/person
				where empty($p/profile/@income)
				return $p)
			}</na>
			</result>
		\end{fakeXML}
			
		
	\end{enumerate}